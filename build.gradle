// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.32"
    ext.hilt_version = '2.35'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath 'com.google.gms:google-services:4.3.8' // read google-services.json file for Firebase
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    // Load `secrets.properties` file, if it exists.
    if (file("$project.rootDir/secrets.properties").exists()) {
        def localProperties = new Properties()
        localProperties.load(new FileInputStream("$project.rootDir/secrets.properties"))
        localProperties.each { prop ->
            project.ext.set(prop.key, prop.value)
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/customerio/customerio-android")
            credentials {
                username = project.ext.find("githubRepo.username") ?: System.getenv("GITHUB_REPO_USERNAME")
                password = project.ext.find("githubRepo.token") ?: System.getenv("GITHUB_REPO_TOKEN")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}